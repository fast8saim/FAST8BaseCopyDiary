// MIT License
//
// Copyright (c) 2023 FAST8.RU
//
// Permission is hereby granted, free of charge, to any person obtaining a copy
// of this software and associated documentation files (the "Software"), to deal
// in the Software without restriction, including without limitation the rights
// to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
// copies of the Software, and to permit persons to whom the Software is
// furnished to do so, subject to the following conditions:
//
// The above copyright notice and this permission notice shall be included in all
// copies or substantial portions of the Software.
//
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
// IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
// FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
// AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
// LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
// OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
// SOFTWARE.

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СформироватьИсторию();
	
КонецПроцедуры // ПриСозданииНаСервере()

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	#Если ТонкийКлиент Тогда
	ВидКлиента = "Тонкий клиент";
	#КонецЕсли
	#Если ТолстыйКлиентУправляемоеПриложение Тогда
	ВидКлиента = "Толстый клиент";
	#КонецЕсли
    #Если ВебКлиент Тогда
	ВидКлиента = "Веб-клиент";
	#КонецЕсли
	
	СИ = Новый СистемнаяИнформация;
	fast8Платформа = СтрШаблон("%1 %2 %3", ВидКлиента, СИ.ВерсияПриложения, СИ.ТипПлатформы);
	
	fast8ЗаписиСобытий.Область(10, 3, 10, 4).Текст = fast8Платформа;
	Элементы.fast8ЗаписиСобытий.ТекущаяОбласть = fast8ЗаписиСобытий.Область(8, 3, 8, 4);
	
КонецПроцедуры // ПриОткрытии()

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "FAST8Дневник_ВведеноСобытие" Тогда
		ДобавитьСобытиеНаСервере(Параметр.Пользователь, Параметр.Описание);
	КонецЕсли;
	
КонецПроцедуры // ОбработкаОповещения()

#КонецОбласти // ОбработчикиСобытийФормы

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ЗаписиСобытийОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка, ДополнительныеПараметры)
	
	ЗарегистрироватьНовоеСобытие();
	
КонецПроцедуры // ЗаписиСобытийОбработкаРасшифровки()

#КонецОбласти // ОбработчикиСобытийЭлементовШапкиФормы

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура fast8ДобавитьСобытие(Команда)
	
	ЗарегистрироватьНовоеСобытие();    
	
КонецПроцедуры // fast8ДобавитьСобытие()

&НаКлиенте
Процедура fast8УказатьДатуАктуальности(Команда)
	
	ПоказатьВводЗначения(Новый ОписаниеОповещения("УказатьДатуАктуальностиЗавершение", ЭтотОбъект, Новый Структура("fast8ДатаАктуальности", fast8ДатаАктуальности)), fast8ДатаАктуальности, "Укажите дату актуальности базы");
	
КонецПроцедуры // fast8УказатьДатуАктуальности()

&НаКлиенте
Процедура fast8УказатьОсновноеНазначение(Команда)
	
	ПоказатьВводЗначения(Новый ОписаниеОповещения("УказатьОсновноеНазначениеЗавершение", ЭтотОбъект, Новый Структура("fast8ОсновноеНазначение", fast8ОсновноеНазначение)), fast8ОсновноеНазначение, "Укажите основное назначение копии");
	
КонецПроцедуры // fast8УказатьОсновноеНазначение()

&НаКлиенте
Процедура fast8ОПрограмме(Команда)
	
	ОткрытьФорму("Обработка.fast8Diary_ДневникКопииБазы.Форма.fast8Diary_ОПрограмме",,,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры // fast8ОПрограмме()

#КонецОбласти // ОбработчикиКомандФормы

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ДобавитьСобытиеНаСервере(Пользователь, Событие)
	
	fast8Diary_ВызовСервера.ДобавитьСобытие(Пользователь, Событие);

	СформироватьИсторию();
	
КонецПроцедуры

&НаСервере
Процедура СформироватьИсторию()

	УстановитьПривилегированныйРежим(Истина);
	
	СохраненнаяДатаПервогоЗапуска = ХранилищеОбщихНастроек.Загрузить("fast8ДатаПервогоЗапуска", "FAST8ДневникКопииБазы",, "FAST8");
	Если СохраненнаяДатаПервогоЗапуска = Неопределено Тогда
		fast8ДатаПервогоЗапуска = ТекущаяДатаСеанса();
		ХранилищеОбщихНастроек.Сохранить("fast8ДатаПервогоЗапуска", "FAST8ДневникКопииБазы", fast8ДатаПервогоЗапуска,, "FAST8");
	Иначе
		fast8ДатаПервогоЗапуска = СохраненнаяДатаПервогоЗапуска;
	КонецЕсли;
	
	Сегодня = НачалоДня(ТекущаяДатаСеанса());
	
	fast8ОсновноеНазначение	= СокрЛП(ХранилищеОбщихНастроек.Загрузить("fast8ОсновноеНазначение", "FAST8ДневникКопииБазы",, "FAST8"));
	
	Если ЗначениеЗаполнено(fast8ОсновноеНазначение) Тогда
		Заголовок = СтрШаблон("%1: история работы с этой копией рабочей базы", Лев(fast8ОсновноеНазначение, 50));
	КонецЕсли;
	
	fast8ДатаАктуальности	= ХранилищеОбщихНастроек.Загрузить("fast8ДатаАктуальности", "FAST8ДневникКопииБазы",, "FAST8");
	ТаблицаСобытий		= ХранилищеОбщихНастроек.Загрузить("fast8События", "FAST8ДневникКопииБазы",, "FAST8");
    Если ТаблицаСобытий <> Неопределено Тогда
		fast8События.Очистить();
		ТаблицаСобытияПользователей = fast8События.Выгрузить();
		ТаблицаВходыВБазу = fast8События.Выгрузить();
		ТаблицаВходыВБазу.Колонки.Добавить("Количество");
		
		Для Каждого СтрокаТЧ Из ТаблицаСобытий Цикл
			Если (СтрокаТЧ.Событие = "Зашел в базу" Или СтрокаТЧ.Событие = "Вошел в базу") И СтрокаТЧ.Дата < Сегодня Тогда
				НовоеСобытие = ТаблицаВходыВБазу.Добавить();
				НовоеСобытие.Дата			= НачалоДня(СтрокаТЧ.Дата);
				НовоеСобытие.Пользователь	= СтрокаТЧ.Пользователь;
				НовоеСобытие.Количество		= 1;
			Иначе
				ЗаполнитьЗначенияСвойств(ТаблицаСобытияПользователей.Добавить(), СтрокаТЧ);				
			КонецЕсли;
		КонецЦикла;
		
		fast8События.Загрузить(ТаблицаСобытияПользователей);
		
		ТаблицаВходыВБазу.Свернуть("Дата, Пользователь", "Количество");
		Для Каждого СтрокаТЧ Из ТаблицаВходыВБазу Цикл
			НовоеСобытие = fast8События.Добавить();
			НовоеСобытие.Дата = СтрокаТЧ.Дата;
			НовоеСобытие.Пользователь = СтрокаТЧ.Пользователь;
			Если СтрокаТЧ.Количество = 1 Тогда
				НовоеСобытие.Событие = "Вошел в базу";
			Иначе
				Склонение = ПолучитьСклоненияСтрокиПоЧислу("раз", СтрокаТЧ.Количество,, "ЧС=Количественное", "ПД=Именительный");
				Раз = ?(Склонение.Количество(), Склонение.Получить(0), "раз");
				НовоеСобытие.Событие = СтрШаблон("Вошел в базу %1", Раз);
			КонецЕсли;
		КонецЦикла;
		
		fast8События.Сортировать("Дата УБЫВ");
	КонецЕсли;
	
	Макет = Обработки.fast8Diary_ДневникКопииБазы.ПолучитьМакет("fast8Diary_Макет");
	fast8ЗаписиСобытий.Очистить();
	
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьШапка.Параметры.ОсновноеНазначение	= fast8ОсновноеНазначение;
	ОбластьШапка.Параметры.ДатаПервогоЗапуска	= fast8ДатаПервогоЗапуска;
	ОбластьШапка.Параметры.ДатаАктуальности		= fast8ДатаАктуальности;
	ОбластьШапка.Параметры.АдресБазы			= СтрокаСоединенияИнформационнойБазы();
	ОбластьШапка.Параметры.ВерсияКонфигурации	= СтрШаблон("%1 %2", Метаданные.Версия, Метаданные.Синоним);
	ОбластьШапка.Параметры.Платформа			= fast8Платформа;
		
	fast8ЗаписиСобытий.Вывести(ОбластьШапка);
	
	Для Каждого СтрокаТЧ Из fast8События Цикл
		
		Если СтрокаТЧ.Дата < Сегодня Тогда
			ФорматДаты = "ДФ=dd.MM.yyyy ЧЧ:мм";
		Иначе
			ФорматДаты = "ДФ=ЧЧ:мм";
		КонецЕсли;
		Если СтрНачинаетсяС(СтрокаТЧ.Событие, "Вошел в базу") Тогда
			ОбластьСтрока = Макет.ПолучитьОбласть("СтрокаСерая");
			Если СтрокаТЧ.Дата < Сегодня Тогда
				ФорматДаты = "ДФ=dd.MM.yyyy";
			КонецЕсли;
		Иначе
			ОбластьСтрока = Макет.ПолучитьОбласть("Строка");
		КонецЕсли;
		ОбластьСтрока.Параметры.Заполнить(СтрокаТЧ);
		ОбластьСтрока.Параметры.Дата = Формат(СтрокаТЧ.Дата, ФорматДаты);
		fast8ЗаписиСобытий.Вывести(ОбластьСтрока);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура УказатьДатуАктуальностиНаСервере()
	
    УстановитьПривилегированныйРежим(Истина);
	ХранилищеОбщихНастроек.Сохранить("fast8ДатаАктуальности", "FAST8ДневникКопииБазы", fast8ДатаАктуальности,, "FAST8");
	
	СформироватьИсторию();
	
КонецПроцедуры

&НаКлиенте
Процедура УказатьДатуАктуальностиЗавершение(Значение, ДополнительныеПараметры) Экспорт
	
	fast8ДатаАктуальности = ?(Значение = Неопределено, ДополнительныеПараметры.fast8ДатаАктуальности, Значение);
	УказатьДатуАктуальностиНаСервере();

КонецПроцедуры

&НаСервере
Процедура УказатьОсновноеНазначениеНаСервере()
	
	УстановитьПривилегированныйРежим(Истина);
	ХранилищеОбщихНастроек.Сохранить("fast8ОсновноеНазначение", "FAST8ДневникКопииБазы", fast8ОсновноеНазначение,, "FAST8");
	
	СформироватьИсторию();
	
КонецПроцедуры

&НаКлиенте
Процедура УказатьОсновноеНазначениеЗавершение(Значение, ДополнительныеПараметры) Экспорт
	
	fast8ОсновноеНазначение = ?(Значение = Неопределено, ДополнительныеПараметры.fast8ОсновноеНазначение, Значение);
	УказатьОсновноеНазначениеНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗарегистрироватьНовоеСобытие()
	
	ОткрытьФорму("Обработка.fast8Diary_ДневникКопииБазы.Форма.fast8Diary_ВводСобытия",, ЭтотОбъект,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры // ЗарегистрироватьНовоеСобытие()

#КонецОбласти // СлужебныеПроцедурыИФункции